/* Author: Hao Wu 
 * A simple query langauge (Q) for debugging and verifying UML models.
 * 28th Feburary, 2019
 */
grammar Query;
options {
  superClass = BaseParser;
}
@header{
package org.tzi.use.query;
import org.tzi.use.query.ast.*;
import org.tzi.use.parser.base.*;
import org.tzi.use.parser.base.BaseParser;
import org.tzi.use.parser.ocl.*;
import org.tzi.use.parser.soil.ast.*;
import java.util.Collections;
import java.util.Arrays;
}

@lexer::header {
package org.tzi.use.query;
import org.tzi.use.query.ast.*;
import org.tzi.use.parser.ParseErrorHandler;
}
@lexer::members {
    private ParseErrorHandler fParseErrorHandler;

    public String getFilename() {
        return fParseErrorHandler.getFileName();
    }
    
    @Override
    public String getErrorHeader(RecognitionException e) {
    	return "line " + e.line + ":" + e.charPositionInLine;
    }
    	
    public void emitErrorMessage(String msg) {
        fParseErrorHandler.reportError(msg);
    }
 
    public void init(ParseErrorHandler handler) {
        fParseErrorHandler = handler;
    }
}

checkExpr returns [QAst expr]:
    qexpr = abstractQueryExpr {$expr=qexpr;} EOF
    |
        mexpr=moduleExpr {$expr = new ModuleListExpr(); ((ModuleListExpr)$expr).addModule(mexpr);} 
        (mexpr=moduleExpr {((ModuleListExpr)$expr).addModule(mexpr);})* EOF
;

abstractQueryExpr returns [AbstractQuery expr]:
    qexpr=queryExpr {$expr=qexpr;}
         (
            ('+' right_expr=queryExpr 
                {
                    $expr = new QueryBinaryExpr($expr, right_expr, Connective.UNION);
                }
            )
        |
            ('-' right_expr=queryExpr 
                {
                    $expr = new QueryBinaryExpr($expr, right_expr, Connective.DIFFER);
                }
            )
        |
            ( '&' right_expr = queryExpr
                {
                    $expr = new QueryBinaryExpr($expr, right_expr, Connective.INTER);
                }
            )
         )*
;

queryExpr returns [QueryExpr qexpr] @init{
    qexpr = new QueryExpr();
}: 
    'select' f=featureExpr {$qexpr.addFeature(f);} (COMMA f=featureExpr {$qexpr.addFeature(f);})* 
        (with=withExpr {$qexpr.addWithExpr(with);})? 
        (without=butExpr {$qexpr.addWithoutExpr(without);})? ('inject' o=injExpr {$qexpr.setOCLExpression(o);})?
        ('as' name=IDENT {$qexpr.setAlias($name.getText());}) ?
   | alias = IDENT {$qexpr.setAlias($alias.getText());$qexpr.setPureAliased();}
   | module=moduleAlias {$qexpr =module;}
;




moduleAlias returns [ModuleAliasExpr maexpr]@init{
    maexpr = new ModuleAliasExpr(); 
}:
    module=IDENT{$maexpr.setModuleName($module.getText());} 
    DOT query=(IDENT| STAR)
    {$maexpr.setQueryName($query.getText());}
;
//queryExpr_nl:
 //   queryExpr '&&' queryExpr
  //  | queryExpr '||' queryExpr
//;
featureExpr returns [QFeatureExpr feature]: 
    (modifier=modifiers) ? dest=(IDENT|STAR) (k=rankExpr)?
    {
        $feature = (k>0) ?
                    new QClassExpr($dest.getText(),modifier,k)
                    :
                    new QClassExpr($dest.getText(),modifier)
                    ;
    }
    | (modifier=modifiers) ? f1 = attrExpr {
            if (modifier!=null) f1.setModifier(modifier);
            $feature=f1;
        }
    | f2 = assocExpr {$feature=f2;}
;

modifiers returns [Modifier m]:
    'only' {$m=Modifier.ONLY;}
    |
    'no' {$m=Modifier.NO;}

    |'all' {$m=Modifier.ALL;}
;

attrExpr returns [QAttrExpr attr]:
    src=(IDENT|STAR) DOT dest=(IDENT|STAR) (k=rankExpr)? 
    {;
        attr = (k>0) ? 
               new QAttrExpr($src.getText(),$dest.getText(),k)
               :
               new QAttrExpr($src.getText(),$dest.getText())
               ;
    }
;

assocExpr returns [QAssocExpr assoc]
: src=(IDENT|STAR) COLON name=(IDENT|STAR) COLON dest=(IDENT|STAR) (k=rankExpr)?
    {
        assoc = (k>0) ? 
                new QAssocExpr($src.getText(),$name.getText(),$dest.getText(),k) 
                :
                new QAssocExpr($src.getText(),$name.getText(),$dest.getText())
                ;
    }
;

withExpr returns [QWithExpr with] @init{
    $with = new QWithExpr();
}: 
    'with' w=invExpr{$with.addInvExpr(w);} (COMMA w=invExpr{$with.addInvExpr(w);})*
;

butExpr returns [QButExpr without] @init{
    $without = new QButExpr();
}
:
    'but' (f=featureExpr {$without.addFeatureExpr(f);}  | w=invExpr {$without.addFeatureExpr(w);}) 
    (COMMA (f=featureExpr {$without.addFeatureExpr(f);} | w=invExpr{$without.addFeatureExpr(w);}) )*
;

invExpr returns [QInvExpr inv]: 
    src=(IDENT|STAR) COLON_COLON dest=(IDENT|STAR) (k=rankExpr)? 
    {
        
        inv =   (k>0) ? 
                new QInvExpr($src.getText(),$dest.getText(),k)
                :
                new QInvExpr($src.getText(),$dest.getText())
                ;
    }
;

rankExpr returns [int rank] @init{
    $rank=0;
 }:
    AT k=INT {$rank=Integer.parseInt($k.text);}
;

moduleExpr returns [ModuleExpr mexpr]:
    'module' name=IDENT {$mexpr = new ModuleExpr($name.getText());}
        query=abstractQueryExpr {$mexpr.addQuery(query);} 
        (query=abstractQueryExpr {$mexpr.addQuery(query);})*
        {query.setModule($mexpr);}
    'end'
;

injExpr returns [QOCLExpr ocl_expr] @init{
    $ocl_expr = new QOCLExpr();
}: 
    LBRACE expr=expression (k=rankExpr)? 
    {
        if (k>0)
            $ocl_expr.addOCLExpression(expr,k);
        else
            $ocl_expr.addOCLExpression(expr);
    } 
    (COMMA expr=expression (k=rankExpr)? 
        {
            if (k>0)
                $ocl_expr.addOCLExpression(expr,k);
            else
                $ocl_expr.addOCLExpression(expr);
        }
    )* RBRACE
;